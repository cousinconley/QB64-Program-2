10000 PRINT "PROGRAM FOR PLOTTING EQUALLY"
10010 PRINT "SPACED DATA VALUES"
10020 PRINT
10030 PRINT "THE USER INPUTS THE STARTING "
10040 PRINT "AND ENDING COORDINATES, ALONG "
10050 PRINT "WITH THE NUMBER OF EVALUATION POINTS."
10060 PRINT "THE PROGRAM WILL THEN PLOT "
10070 PRINT "THE DATA"
10080 PRINT
10090 PRINT
10100 REM INITIALIZATION
10110 PRINT "INPUT TERMINAL WIDTH: ",
10120 INPUT L
10130 PRINT "BEGINNING COORDINATE: ",
10140 INPUT X1
10150 PRINT "ENDING COORDINATE: ",
10160 INPUT X2
10170 PRINT "NUMBER OF DATA POINTS: ",
10180 INPUT N
10190 DIM D(N + 1)
10200 REM DATA INPUT
10210 PRINT "INPUT DATA"
10220 FOR I = 1 TO N
    10230 PRINT I,
    10240 INPUT D(I)
10250 NEXT I
10260 REM GO TO PLOTTING SUBROUTINE PROPER
10270 GOSUB 40000
10280 END
39998 REM PLOTTING SUBROUTINE (EQAPLOT)
39999 REM SHIFT DATA TO NON-NEGATIVE
40000 B = 100000000
40001 REM FIND MINIMUM DATA VALUE
40002 FOR I = 1 TO N
    40003 IF B > D(I) THEN B = D(I)
40004 NEXT I
40005 REM SUBTRACT MINIMUM VALUE FROM ALL DATA
40006 FOR I = 1 TO N
    40007 D(I) = D(I) - B
40008 NEXT I
40009 REM FIND MAX. SHIFTED DATA VALUE
40010 C = 0
40011 FOR I = 1 TO N
    40012 IF C < D(I) THEN C = D(I)
40013 NEXT I
40014 REM DETERMINE PRINTING SCALE VALUE
40015 A = L / C
40016 REM FIND TAB POSITION OF ZERO
40017 E = A * ABS(B)
40018 PRINT
40019 PRINT
40020 PRINT "***** DATA PLOT (SCALED) *****"
40021 PRINT
40022 PRINT
40023 PRINT "MIN. ORDINATE= ", B, " MAX. ORDINATE= ", C + B
40024 PRINT "INITIAL ABSCISSA VALUE= ", X1
40025 PRINT
40026 PRINT
40027 REM IF B IS POSITIVE, SKIP ZERO LABEL
40028 IF B > 0 THEN GOTO 40034
40029 REM IF DATA ARE ALL BELOW ZERO, SKIP LABEL
40030 IF ABS(B) > C THEN GOTO 40034
40031 REM LABEL ZERO
40032 PRINT TAB(E), "0"
40033 REM GO TO AXIS PRINT SUBROUTINE
40034 GOSUB 40200
40035 FOR I = 1 TO N
    40036 REM INSERT LINE FEED FOR AUTO SPACING
    40037 FOR K = 1 TO (INT(0.6 * L / N))
        40038 PRINT ":", TAB(L), ":"
    40039 NEXT K
    40040 REM LOCATE DATUM POSITION
    40041 E2 = A * D(I)
    40042 REM FORMATTED PRINT
    40043 IF E2 > 1 THEN GOTO 40046
    40044 PRINT "*"
    40045 GOTO 40049
    40046 PRINT ":"
    40047 PRINT TAB(E2), "*"
    40048 IF INT(E2) = L THEN GOTO 40050
    40049 PRINT TAB(L), ":",
    40050 PRINT
40051 NEXT I
40052 REM GO TO AXIS PRINT SUBROUTINE
40053 GOSUB 40200
40054 PRINT
40055 PRINT
40056 PRINT "END ABSCISSA VALUE= ", X2
40057 PRINT
40058 PRINT
40059 REM RETURN TO DATA SOURCE PROGRAM
40060 RETURN
40199 REM AXIS PLOT (AXISPLOT)
40200 E3 = E - 5 * INT(E / 5)
40201 REM IF B IS POSITIVE, THEN SKIP ZERO
40202 IF B > 0 THEN E3 = 0
40203 REM IF B IS GREATER THAN THE LARGEST VALUE, SKIP
40204 IF ABS(B) > C THEN E3 = 0
40205 FOR J = 1 TO E3
    40206 PRINT "-",
40207 NEXT J
40208 FOR J = 1 TO (L - E3) / 5
    40209 PRINT "I----",
40210 NEXT J
40211 PRINT "I",
40212 E4 = (J - 1) * 5 + 1 + E3
40213 IF E4 = K + 1 THEN PRINT
40214 IF E4 = L + 1 THEN GOTO 40221
40215 E4 = E4 + 1
40216 IF E4 >= L + 1 THEN GOTO 40219
40217 PRINT "-",
40218 GOTO 40215
40219 PRINT ":"
40220 REM RETURN TO MAIN PLOTTING PROGRAM
40221 RETURN




